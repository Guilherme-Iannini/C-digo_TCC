#include <WiFi.h>
#include <IOXhop_FirebaseESP32.h>
#include <ArduinoJson.h>

#define FIREBASE_HOST "https://tcc-guilherme-iannini-cd725-default-rtdb.firebaseio.com"
#define FIREBASE_AUTH "h2dXQeBAH6jwjxRZFrLlLJXBS3m136GXiq7DbJGM"

const char* ssid = "Gui";
const char* password = "guiga123";

#define ANALOG_PIN_0 35
#define PINO_RELE 27

const float tensaoAC = 127.0;
const float sensibilidadeSensor = 0.1;
const float kilowattHora = 0.8;

float correnteRms = 0;
float potencia = 0;
unsigned long tempoLigado = 0;
float valorTotal = 0;

String statusEquipamento = "desligado";
String statusAnterior = "";

unsigned long ultimoCiclo = 0;
const unsigned long intervaloCiclo = 5000;  // 5 segundos
int contadorCiclos = 0;

void configurarWiFi() {
  WiFi.begin(ssid, password);
  Serial.print("Conectando ao Wi-Fi...");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("\nWi-Fi conectado.");
}

float calcularCorrenteRMS() {
  float somaQuadrados = 0;
  float filtro;
  for (int i = 0; i < 500; i++) {
    int leitura = analogRead(ANALOG_PIN_0) - 1414;
    somaQuadrados += leitura * leitura;
    delay(1);
  }
  float aux = sqrt(somaQuadrados / 500) * (2.5 / 4096);
  float corrente = aux / sensibilidadeSensor;
  if (corrente <= 0.5) {
    return corrente;
  } else {
    filtro =(1.6756 * corrente * corrente) - (1.2907 * corrente) + 0.6432;
    return filtro;
  }
}

void atualizarStatusRele(String status) {
  if (status == "Ligado") {
    digitalWrite(PINO_RELE, LOW);  // Relé ativado
  } else {
    digitalWrite(PINO_RELE, HIGH); // Relé desativado
  }
}

void setup() {
  Serial.begin(115200);
  configurarWiFi();
  Firebase.begin(FIREBASE_HOST, FIREBASE_AUTH);

  pinMode(PINO_RELE, OUTPUT);
  digitalWrite(PINO_RELE, HIGH); // Desligado por padrão

  int tempoLido = Firebase.getInt("/Tomada1/Tempo_de_Uso/Segundos");
  tempoLigado = (tempoLido >= 0) ? tempoLido : 0;
}

void loop() {
  unsigned long tempoAtual = millis();

  if (tempoAtual - ultimoCiclo >= intervaloCiclo) {
    ultimoCiclo = tempoAtual;
    contadorCiclos++;

    // Lê status do Firebase e limpa formatação
    String statusLido = Firebase.getString("/Tomada1/Status");
    statusLido.trim();
    statusLido.replace("\\", "");
    statusLido.replace("\"", "");

    if (statusLido.length() > 0 && statusLido != statusAnterior) {
      statusEquipamento = statusLido;
      statusAnterior = statusLido;
      Serial.print("Novo status recebido: ");
      Serial.println(statusEquipamento);
    }

    atualizarStatusRele(statusEquipamento);

    // Medir corrente e potência
    correnteRms = calcularCorrenteRMS();
    potencia = correnteRms * tensaoAC;

    // Acumula tempo ligado (5 segundos por ciclo)
    tempoLigado += 5;

    // Só envia dados ao Firebase a cada 2 ciclos (10 segundos)
    if (contadorCiclos >= 2) {
      contadorCiclos = 0;

      float consumoSegundo = (potencia / 1000.0) * (1.0 / 3600.0);
      float valorPorSegundo = consumoSegundo * kilowattHora;
      float valorPorMinuto = valorPorSegundo * 60.0;
      float valorPorHora = valorPorMinuto * 60.0;
      float valorPorDia = valorPorHora * 24.0;

      float valorTotalAntigo = Firebase.getFloat("/Tomada1/Valor/Total_Contabilizado");
      valorTotal = (isnan(valorTotalAntigo)) ? 0 : valorTotalAntigo + (valorPorSegundo * 10);
      Firebase.setFloat("/Tomada1/Valor/Total_Contabilizado", valorTotal);

      int dias = tempoLigado / 86400;
      int horas = (tempoLigado % 86400) / 3600;
      int minutos = (tempoLigado % 3600) / 60;
      int segundos = tempoLigado % 60;

      Firebase.setInt("/Tomada1/Tempo_de_Uso/Segundos", segundos);
      Firebase.setInt("/Tomada1/Tempo_de_Uso/Minutos", minutos);
      Firebase.setInt("/Tomada1/Tempo_de_Uso/Horas", horas);
      Firebase.setInt("/Tomada1/Tempo_de_Uso/Dias", dias);

      Firebase.setFloat("/Tomada1/Leitura/Corrente", correnteRms);
      Firebase.setFloat("/Tomada1/Leitura/Potencia", potencia);
      Firebase.setFloat("/Tomada1/Valor/Por_Minuto", valorPorMinuto);
      Firebase.setFloat("/Tomada1/Valor/Por_Hora", valorPorHora);
      Firebase.setFloat("/Tomada1/Valor/Por_Dia", valorPorDia);

      Serial.println("--- Dados enviados ao Firebase ---");
      Serial.print("Corrente RMS: ");
      Serial.print(correnteRms);
      Serial.println(" A");

      Serial.print("Potência: ");
      Serial.print(potencia);
      Serial.println(" W");

      Serial.print("Tempo ligado: ");
      Serial.print(tempoLigado);
      Serial.println(" s");

      Serial.print("Valor total acumulado: R$ ");
      Serial.println(valorTotal);
    }
  }
}
